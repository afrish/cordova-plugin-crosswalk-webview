/*
       Licensed to the Apache Software Foundation (ASF) under one
       or more contributor license agreements.  See the NOTICE file
       distributed with this work for additional information
       regarding copyright ownership.  The ASF licenses this file
       to you under the Apache License, Version 2.0 (the
       "License"); you may not use this file except in compliance
       with the License.  You may obtain a copy of the License at

         http://www.apache.org/licenses/LICENSE-2.0

       Unless required by applicable law or agreed to in writing,
       software distributed under the License is distributed on an
       "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
       KIND, either express or implied.  See the License for the
       specific language governing permissions and limitations
       under the License.
*/

def DEFAULT_MIN_SDK_VERSION = 19

def getConfigPreference(name) {
    name = name.toLowerCase()
    // def xml = file("res/xml/config.xml").getText()
    def xml = file("src/main/res/xml/config.xml").getText()
    // Disable namespace awareness since Cordova doesn't use them properly
    def root = new XmlParser(false, false).parseText(xml)

    def ret, defaultValue
    root.preference.each { it ->
        def attrName = it.attribute("name")
        if (attrName && attrName.toLowerCase() == name) {
            if (it.attribute('default') != null) {
                defaultValue = it.attribute('default');
            } else {
                ret = it.attribute("value")
            }
        }
    }
    return ret ? ret : defaultValue
}

def minSdk = getConfigPreference("android-minSdkVersion");
if (cdvMinSdkVersion == null) {
    ext.cdvMinSdkVersion = minSdk && Integer.parseInt(minSdk) > DEFAULT_MIN_SDK_VERSION ? minSdk : DEFAULT_MIN_SDK_VERSION;
} else if (Integer.parseInt('' + cdvMinSdkVersion) < Integer.parseInt(minSdk)) {
    ext.cdvMinSdkVersion = minSdk;
}

ext.cdvBuildMultipleApks = true;

if (!project.hasProperty('xwalkCommandLine')) {
    ext.xwalkCommandLine = getConfigPreference("xwalkCommandLine")
}

// Apply values after project's build-extras.gradle
cdvPluginPostBuildExtras.add({
    repositories {
      maven {
        url 'https://download.01.org/crosswalk/releases/crosswalk/android/maven2'
      }
    }

    dependencies {
        compile 'org.xwalk:xwalk_core_library:23+'
        compile 'org.xwalk:xwalk_core_library:23+:64bit@aar'
    }

    if (file('assets/xwalk-command-line').exists()) {
        println('Not writing assets/xwalk-command-line since file already exists.')
        return
    }
    android.applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def mergeTask = tasks["merge${variantName}Assets"]
        def processTask = tasks["process${variantName}Resources"]
        def outFile = new File (mergeTask.outputDir, "xwalk-command-line")
        def newTask = project.task("createXwalkCommandLineFile${variantName}") << {
            mergeTask.outputDir.mkdirs()
            outFile.write("xwalk ${xwalkCommandLine}\n")
        }
        newTask.dependsOn(mergeTask)
        processTask.dependsOn(newTask)
    }
})
